windows? if utf8>sjis then

`help : <<<
	----------------------------------
	countPrimeMT-short.pp {run | test}
	----------------------------------
	This program examines the number of primes that exist between 1 and 10 million.
	It uses multi-threads and runs the investigation in parallel. It takes about 19
	seconds on a Mac book air (Intel CPU) with 4 CPU threads.

	Usage: In the samples directory
    \    ... /para countPrimeMT-short.pp run

	and you should get the following output.
    \    numOfPrimes=664579 (2 ... 9999991)
  >>> write
;

"isPrime.p8e" load

// n --- t/f
`prime?-for-odd :
	dup sqrt ceil >int 1 + 3 swap
	for+ dup i % 0? if drop false return then 2 step next
	drop true
;

interactive? not if
	args size 0? if
      help cr -1 exit
    then 
then

`calc : 
	reset-pipes
	// 奇数については並列処理で調べる
	[ 3 10000000 for+ i >pipe 2 step next ]

	[[	// 複数スレッドで検証作業（=woker thrread)
		while-pipe
			dup prime?-for-odd if >pipe else drop then
		repeat
	]]

	( 2 ) while-pipe @append repeat { < } sort
;

`run :
	calc
	( "numOfPrimes=%d (%d ... %d)\n" )
		over size @append
		over car  @append
		over last @append
	printf
	drop
;

`check : != if "NG" .cr -1 exit then ;

`test :
	calc
	@size 664579  check	// num of primes in [1,10^7].
	@car  2       check	// 1st prime
	@last 9999991 check	// last prime in [1,10^7].
	drop
	"GOOD" .cr
;

