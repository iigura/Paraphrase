// usage: para dumpMPmt.pp

`help : <<<

	Searches for and displays the Mersenne primes up to the 20th number.

	\tusage: ../para dumpMPmt.p8e {run | test}

	>>> write
;

interactive? not if
	args size 0? if help then
then

"isPrime.p8e" load

`mersenne : 2LL swap pow 1 - ;

`lucas-lehmer-test-fast-by-stack :
	dup 1 over mersenne	rot	2 - rot swap 4LL inv-rot 1 +
	for+
		dup * dup 3 pick >>	swap 2 pick	& +	dup	2 pick	
		>= if over - then
		2 -	
	next
	0 == inv-rot drop drop
;

// p lucas-lehmer-test-fast
`lucas-lehmer-test-fast :
	`p local
	4LL `s local
	2LL `p , pow 1 - `M local
	`sqrt local-decl
	2LL `p , for+
		`s , dup * `sqrt let
		`sqrt , `M , & `sqrt , `p , >> + `s let
		`s , `M , >= if `s , `M , - `s let then
		`s , 2 - `s let
	next
	`s , 0 ==
;

`num-of-digit :
	dup log10 floor >int 1 + dup 4 +
	for+ dup 10LL i >int pow < if drop i 1 - leave then next
	1 +
;

// n ---
`calc-and-print-mersenne :
	dup "p=%5d, " putf
	mersenne dup num-of-digit dup 13 > if
		dup inv-rot
		5 - 10LL swap pow swap dup rot / write	
		"..." write 10 5 pow % "%05d" putf " (%5d digits)" putf
	else
		drop "%13d" putf
	then
;

`N var

// check until $N
`calc :
	// check 1...4 by single-thread
	() 1 5 for+
		i prime? if
			i mersenne prime? if i @append then
		then
	next

	// check 4...5000 by multi-thread
	reset-pipes
	[ 5 $N> for+ i >pipe 2 step next ]
	[[
		while-pipe
			dup prime? if
				dup lucas-lehmer-test-fast if >pipe then
			then
		repeat
	]]
	while-pipe @append repeat { < } sort
;

// T ---
`dump-result :
	0 >r {
		r> 1 + dup "No=%2d, " putf >r
		calc-and-print-mersenne cr
	} foreach
;

`run  :  5001 >$N calc dump-result ;
`run2 : 10001 >$N calc dump-result ;
`run3 : 20001 >$N calc dump-result ;

`check : != if "NG" .cr -1 exit then ;

`test :
	5001 >$N
	calc
	last 4423 check
	4423 mersenne 
	dup num-of-digit 1332 check
	dup 10LL 1332 5 - pow / 28554 check
	100000 % 80607 check
	"GOOD" .cr
;

